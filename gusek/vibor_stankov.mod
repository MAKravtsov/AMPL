#множество обрабатываемых деталей
set I;
#множество типов станков
set J;
#переменная = оптимизируемому функционалу
var Y; #,>=0;

#допустимое число станков каждого типа
param M_j{J};
set M_J{j in J} := if M_j[j]>0 then {1 .. M_j[j]} else {};

#число маршрутов по которым может быть обработана деталь
param K_i{I};
set K_I{i in I} := if K_i[i]>0 then {0..K_i[i]-1} else  {};

#множество деталей обрабатываемых на определенных станках
set I_j{J}, dimen 1;

set K_j_i{j in J,i in I_j[j]}, dimen 1;

#переменная-индикатор производства детали по определенному техмаршруту
var th_ik{i in I, k in K_I[i]},binary;

#доля от общей партии детали обрабатываемой детали по определенному техмаршруту на определенном станке
var th_tilda_jilk{j in J,i in I_j[j],l in M_J[j], k in K_j_i[j,i]} >=0,<=1;

#число деталей в партии
param n_i{I};

#время операций обработки деталей на станке определенного типа
param t_jik{j in J, i in I_j[j],k in K_j_i[j,i]};

#переменная-индикатор производства детали по определенному техмаршруту на определенном станке
var th_hat_jilk{j in J,i in I_j[j],l in M_J[j], k in K_j_i[j,i]}, binary;

#время переналадки оборудования
param tau_jik{j in J, i in I_j[j],k in K_j_i[j,i]};

#переменная включения-исключения станка
var Y_jl{j in J, l in M_J[j]}, binary;

#ресурс времени станка j-го типа
param V_j{J};

#максимальный коэффициент загрузки станка j-го типа
param mu_j{J};

#отпускная цена детали
param  c_i{i in I};

#себестоимость производства детали по k-му маршруту
param c_ik{i in I, k in K_I[i]};

#минимальное значение желаемой
param C_0;
#средства выделенные на закупку оборудования
param D;

param d_j{J};

#Стоимость комплекта технологической оснастки
param b_jik{j in J, i in I_j[j],k in K_j_i[j,i]},default 0;



param a1, default 1.0;
param a2, default 1.0;
param a3, default 1.0;

#стоимость обслуживания станка за выбранный период времени
param b_j{J};

#что максимизируем
maximize target: Y;

#оптимизируемый функционал
s.t. funct: Y = 
a1*sum{i in I, k in K_I[i]} n_i[i]*th_ik[i,k]*(c_i[i]-c_ik[i,k])

-a2*sum{j in J, l in M_J[j]} (b_j[j]*Y_jl[j,l])

-a3*sum{j in J, i in I_j[j],l in M_J[j],k in K_j_i[j,i]} (b_jik[j,i,k]*th_hat_jilk[j,i,l,k]);

#ограничение по времени работы
s.t. TimeLimit 
{j in J, l in M_J[j]}:

sum{i in I_j[j],k in K_j_i[j,i]}(th_tilda_jilk[j,i,l,k]*n_i[i]*t_jik[j,i,k]+th_hat_jilk[j,i,l,k]*tau_jik[j,i,k])
	<=Y_jl[j,l]*V_j[j]*mu_j[j];

s.t. OneRoute{j in J, i in I_j[j], k in K_j_i[j,i]}:

sum{l in M_J[j]}th_tilda_jilk[j,i,l,k]=th_ik[i,k];

#все детали являются обязательными, исключать детали нельзя
# necessary detail condition
s.t. ndc{i in I}:

sum{k in K_I[i]}th_ik[i,k]=1;

# учет переналадок оборудования
s.t. retuning{j in J, l in M_J[j], i in I_j[j], k in K_j_i[j,i]}:
			th_hat_jilk[j,i,l,k]-th_tilda_jilk[j,i,l,k]>=0;

#ограничение на прибыль
s.t. income: 

			sum{i in I, k in K_I[i]}n_i[i]*th_ik[i,k]*(c_i[i]-c_ik[i,k])>=C_0;
			
#ограничение на стоимость оборудования
s.t. cost: 
			sum{j in J, l in M_J[j]}
			(
					d_j[j]*Y_jl[j,l]
					+sum{i in I_j[j],k in K_j_i[j,i]}b_jik[j,i,k]*th_hat_jilk[j,i,l,k]
			)
				<=D;
solve;


#вывод результатов моделирования в файл

display Y;

printf '' > 'out.txt';

for{j in J,i in I_j[j],l in M_J[j], k in K_j_i[j,i]:th_tilda_jilk[j,i,l,k]>0}
{
   printf '%s;', j >> 'out.txt';
   printf '%s;', i	 >> 'out.txt';
   printf '%d;', k >> 'out.txt';
   printf '%f;', th_tilda_jilk[j,i,l,k]>> 'out.txt';
   printf '%d', l >> 'out.txt';
   printf "\n"  >> 'out.txt';
}
printf "\n"  >> 'out.txt';
printf 'Сколько мы потратили? %d',sum{j in J, l in M_J[j]}
			(
					d_j[j]*Y_jl[j,l]
					+sum{i in I_j[j],k in K_j_i[j,i]}b_jik[j,i,k]*th_hat_jilk[j,i,l,k]
			)>> 'out.txt';
printf "\n"  >> 'out.txt';
printf 'Прибыль = %d',sum{i in I, k in K_I[i]}n_i[i]*th_ik[i,k]*(c_i[i]-c_ik[i,k])>> 'out.txt';

data;
#Множество станков и деталей
set J:= "Расточный" "Фрезерный" "Моечный" "Зуборезной" "Вертикально-сверлильный" "Радиально-сверлильный" "Токарно-винторезный" "Строгальный" "Продольно-фрезерный" "Станок с ЧПУ" ;
set I:= "Вал" "Корпус" "Втулка" "Ступица" "Стакан" "Колесо" "Рычаг" "Штуцер" "Клапан" "Крышка" ;
#Деньги
param C_0:=0;
param D:=1000000000000;
#Параметры станка
param b_j:=
"Расточный" 100000
"Фрезерный" 80000
"Моечный" 50000
"Зуборезной" 90000
"Вертикально-сверлильный" 130000
"Радиально-сверлильный" 145000
"Токарно-винторезный" 130000
"Строгальный" 30000
"Продольно-фрезерный" 130000
"Станок с ЧПУ" 200000
;
param d_j:=
"Расточный" 1000000
"Фрезерный" 3000000
"Моечный" 500000
"Зуборезной" 3000000
"Вертикально-сверлильный" 2000000
"Радиально-сверлильный" 1800000
"Токарно-винторезный" 2500000
"Строгальный" 700000
"Продольно-фрезерный" 3000000
"Станок с ЧПУ" 5000000
;
param V_j:=
"Расточный" 1000000
"Фрезерный" 1500000
"Моечный" 2000000
"Зуборезной" 1500000
"Вертикально-сверлильный" 1300000
"Радиально-сверлильный" 1500000
"Токарно-винторезный" 1100000
"Строгальный" 1600000
"Продольно-фрезерный" 1500000
"Станок с ЧПУ" 2500000
;
param mu_j:=
"Расточный" 0.85
"Фрезерный" 0.85
"Моечный" 0.85
"Зуборезной" 0.85
"Вертикально-сверлильный" 0.85
"Радиально-сверлильный" 0.85
"Токарно-винторезный" 0.85
"Строгальный" 0.85
"Продольно-фрезерный" 0.85
"Станок с ЧПУ" 0.85
;
param M_j:=
"Расточный" 50
"Фрезерный" 50
"Моечный" 50
"Зуборезной" 50
"Вертикально-сверлильный" 50
"Радиально-сверлильный" 50
"Токарно-винторезный" 50
"Строгальный" 50
"Продольно-фрезерный" 50
"Станок с ЧПУ" 50
;
#Параметры деталей
param K_i:=
"Вал" 2
"Корпус" 2
"Втулка" 1
"Ступица" 1
"Стакан" 2
"Колесо" 1
"Рычаг" 2
"Штуцер" 2
"Клапан" 1
"Крышка" 1
;
param c_i:=
"Вал" 10000
"Корпус" 20000
"Втулка" 500
"Ступица" 10000
"Стакан" 1500
"Колесо" 2000
"Рычаг" 8000
"Штуцер" 300
"Клапан" 700
"Крышка" 15000
;
param n_i:=
"Вал" 50
"Корпус" 30
"Втулка" 500
"Ступица" 40
"Стакан" 200
"Колесо" 100
"Рычаг" 60
"Штуцер" 600
"Клапан" 250
"Крышка" 35
;
#Какие детали, на каких станках обрабатываются
set I_j["Расточный"]:= "Вал" "Корпус" "Втулка" "Стакан" "Колесо" "Рычаг" "Клапан" ;
set I_j["Фрезерный"]:= "Корпус" "Ступица" "Рычаг" "Крышка" ;
set I_j["Моечный"]:= "Вал" "Корпус" "Стакан" "Колесо" "Крышка" ;
set I_j["Зуборезной"]:= "Колесо" ;
set I_j["Вертикально-сверлильный"]:= "Корпус" "Стакан" "Рычаг" ;
set I_j["Радиально-сверлильный"]:= "Корпус" "Клапан" ;
set I_j["Токарно-винторезный"]:= "Вал" "Втулка" "Стакан" "Штуцер" ;
set I_j["Строгальный"]:= "Колесо" "Рычаг" "Клапан" "Крышка" ;
set I_j["Продольно-фрезерный"]:= "Корпус" "Ступица" "Рычаг" ;
set I_j["Станок с ЧПУ"]:= "Вал" "Ступица" "Рычаг" "Штуцер" ;
#Маршруты деталей
set K_j_i["Расточный","Вал"]:= 0 ;
set K_j_i["Моечный","Вал"]:= 0 1 ;
set K_j_i["Токарно-винторезный","Вал"]:= 0 ;
set K_j_i["Станок с ЧПУ","Вал"]:= 1 ;
set K_j_i["Расточный","Корпус"]:= 0 ;
set K_j_i["Фрезерный","Корпус"]:= 0 ;
set K_j_i["Моечный","Корпус"]:= 0 1 ;
set K_j_i["Вертикально-сверлильный","Корпус"]:= 0 ;
set K_j_i["Радиально-сверлильный","Корпус"]:= 1 ;
set K_j_i["Продольно-фрезерный","Корпус"]:= 1 ;
set K_j_i["Расточный","Втулка"]:= 0 ;
set K_j_i["Токарно-винторезный","Втулка"]:= 0 ;
set K_j_i["Фрезерный","Ступица"]:= 0 ;
set K_j_i["Продольно-фрезерный","Ступица"]:= 0 ;
set K_j_i["Станок с ЧПУ","Ступица"]:= 0 ;
set K_j_i["Расточный","Стакан"]:= 0 1 ;
set K_j_i["Моечный","Стакан"]:= 0 1 ;
set K_j_i["Вертикально-сверлильный","Стакан"]:= 1 ;
set K_j_i["Токарно-винторезный","Стакан"]:= 0 ;
set K_j_i["Расточный","Колесо"]:= 0 ;
set K_j_i["Моечный","Колесо"]:= 0 ;
set K_j_i["Зуборезной","Колесо"]:= 0 ;
set K_j_i["Строгальный","Колесо"]:= 0 ;
set K_j_i["Расточный","Рычаг"]:= 0 ;
set K_j_i["Фрезерный","Рычаг"]:= 1 ;
set K_j_i["Вертикально-сверлильный","Рычаг"]:= 0 ;
set K_j_i["Строгальный","Рычаг"]:= 0 1 ;
set K_j_i["Продольно-фрезерный","Рычаг"]:= 0 ;
set K_j_i["Станок с ЧПУ","Рычаг"]:= 1 ;
set K_j_i["Токарно-винторезный","Штуцер"]:= 0 ;
set K_j_i["Станок с ЧПУ","Штуцер"]:= 1 ;
set K_j_i["Расточный","Клапан"]:= 0 ;
set K_j_i["Радиально-сверлильный","Клапан"]:= 0 ;
set K_j_i["Строгальный","Клапан"]:= 0 ;
set K_j_i["Фрезерный","Крышка"]:= 0 ;
set K_j_i["Моечный","Крышка"]:= 0 ;
set K_j_i["Строгальный","Крышка"]:= 0 ;
#Время обработки
param t_jik
["Расточный","Вал",0]:= 60
["Моечный","Вал",0]:= 30
["Моечный","Вал",1]:= 45
["Токарно-винторезный","Вал",0]:= 120
["Станок с ЧПУ","Вал",1]:= 150
["Расточный","Корпус",0]:= 100
["Фрезерный","Корпус",0]:= 120
["Моечный","Корпус",0]:= 30
["Моечный","Корпус",1]:= 30
["Вертикально-сверлильный","Корпус",0]:= 30
["Радиально-сверлильный","Корпус",1]:= 45
["Продольно-фрезерный","Корпус",1]:= 150
["Расточный","Втулка",0]:= 5
["Токарно-винторезный","Втулка",0]:= 20
["Фрезерный","Ступица",0]:= 120
["Продольно-фрезерный","Ступица",0]:= 45
["Станок с ЧПУ","Ступица",0]:= 150
["Расточный","Стакан",0]:= 30
["Расточный","Стакан",1]:= 30
["Моечный","Стакан",0]:= 25
["Моечный","Стакан",1]:= 35
["Вертикально-сверлильный","Стакан",1]:= 40
["Токарно-винторезный","Стакан",0]:= 50
["Расточный","Колесо",0]:= 40
["Моечный","Колесо",0]:= 20
["Зуборезной","Колесо",0]:= 180
["Строгальный","Колесо",0]:= 25
["Расточный","Рычаг",0]:= 30
["Фрезерный","Рычаг",1]:= 35
["Вертикально-сверлильный","Рычаг",0]:= 35
["Строгальный","Рычаг",0]:= 40
["Строгальный","Рычаг",1]:= 40
["Продольно-фрезерный","Рычаг",0]:= 45
["Станок с ЧПУ","Рычаг",1]:= 25
["Токарно-винторезный","Штуцер",0]:= 20
["Станок с ЧПУ","Штуцер",1]:= 12
["Расточный","Клапан",0]:= 11
["Радиально-сверлильный","Клапан",0]:= 15
["Строгальный","Клапан",0]:= 9
["Фрезерный","Крышка",0]:= 240
["Моечный","Крышка",0]:= 60
["Строгальный","Крышка",0]:= 30
;
#Время переналадки
param tau_jik
["Расточный","Вал",0]:= 7
["Моечный","Вал",0]:= 7
["Моечный","Вал",1]:= 7
["Токарно-винторезный","Вал",0]:= 7
["Станок с ЧПУ","Вал",1]:= 7
["Расточный","Корпус",0]:= 5
["Фрезерный","Корпус",0]:= 10
["Моечный","Корпус",0]:= 5
["Моечный","Корпус",1]:= 5
["Вертикально-сверлильный","Корпус",0]:= 10
["Радиально-сверлильный","Корпус",1]:= 10
["Продольно-фрезерный","Корпус",1]:= 5
["Расточный","Втулка",0]:= 7
["Токарно-винторезный","Втулка",0]:= 5
["Фрезерный","Ступица",0]:= 10
["Продольно-фрезерный","Ступица",0]:= 10
["Станок с ЧПУ","Ступица",0]:= 15
["Расточный","Стакан",0]:= 10
["Расточный","Стакан",1]:= 9
["Моечный","Стакан",0]:= 10
["Моечный","Стакан",1]:= 7
["Вертикально-сверлильный","Стакан",1]:= 7
["Токарно-винторезный","Стакан",0]:= 6
["Расточный","Колесо",0]:= 10
["Моечный","Колесо",0]:= 5
["Зуборезной","Колесо",0]:= 20
["Строгальный","Колесо",0]:= 15
["Расточный","Рычаг",0]:= 10
["Фрезерный","Рычаг",1]:= 5
["Вертикально-сверлильный","Рычаг",0]:= 7
["Строгальный","Рычаг",0]:= 7
["Строгальный","Рычаг",1]:= 7
["Продольно-фрезерный","Рычаг",0]:= 7
["Станок с ЧПУ","Рычаг",1]:= 10
["Токарно-винторезный","Штуцер",0]:= 7
["Станок с ЧПУ","Штуцер",1]:= 5
["Расточный","Клапан",0]:= 7
["Радиально-сверлильный","Клапан",0]:= 7
["Строгальный","Клапан",0]:= 7
["Фрезерный","Крышка",0]:= 15
["Моечный","Крышка",0]:= 5
["Строгальный","Крышка",0]:= 10
;
#Себестоимость деталей
param c_ik
["Вал",0]:= 5000
["Вал",1]:= 6000
["Корпус",0]:= 7500
["Корпус",1]:= 7750
["Втулка",0]:= 100
["Ступица",0]:= 3500
["Стакан",0]:= 300
["Стакан",1]:= 400
["Колесо",0]:= 650
["Рычаг",0]:= 1200
["Рычаг",1]:= 1150
["Штуцер",0]:= 75
["Штуцер",1]:= 80
["Клапан",0]:= 125
["Крышка",0]:= 8000
;
